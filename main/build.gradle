buildscript {
    repositories {
        maven { url 'http://repo1.maven.org/maven2' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}

apply plugin: 'android'

project.archivesBaseName = "dashclock"

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'com.android.support:support-v13:18.0.0'
    compile 'com.google.android.gms:play-services:4.0.30'
    compile 'com.mobeta.android.dslv:drag-sort-listview:0.6.1-SNAPSHOT-AAR'
    compile project(':api')
}

android {
    compileSdkVersion 19
    buildToolsVersion rootProject.ext.buildToolsVersion

    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('version.properties')))

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 19

        versionName versionProps['name']
        versionCode versionProps['code'].toInteger()
    }

//    sourceSets {
//        main {
//            java {
//                srcDir '../api/src/main/java'
//            }
//            aidl {
//                srcDir '../api/src/main/aidl'
//            }
//        }
//    }

    signingConfigs {
        release {
            def Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            def Properties keyProps = new Properties()
            assert localProps['keystore.props.file'];
            keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }

    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
            signingConfig signingConfigs.release
        }

        publicBeta.initWith(buildTypes.release)
        publicBeta {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
            versionNameSuffix " Beta " + versionProps['betaNumber']
        }

        publicDebug.initWith(buildTypes.publicBeta)
        publicDebug {
            debuggable true
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
            versionNameSuffix " Debug Beta " + versionProps['betaNumber']
        }
    }
}
